configure(subprojects.findAll { it.name.contains("java/") }) {

	apply plugin: 'maven-publish'
	apply plugin: 'bintray'
	/////////////////////// publishing /////////////////////////

	def pomConfig = {
		licenses {
			license {
				name "The Apache Software License, Version 2.0"
				url "http://www.apache.org/licenses/LICENSE-2.0.txt"
				distribution "repo"
			}
		}
		developers {
			developer {
				id "otrosien"
				name "Oliver Trosien"
				email "otrosien@epages.com"
			}
		}
	}

    def pomFile = file('build/' + archivesBaseName.replaceAll("java/","soapclient-") + '-' + version + '.pom')

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifactId = project.archivesBaseName
                artifact sourcesJar {
                    classifier "sources"
                }

                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', project.description)
                    root.children().last() + pomConfig
                }
            }
        }
    }

    model {
        generatePomFileForMavenJavaPublication {
            destination = pomFile
        }
    }

    project.artifacts {
        archives pomFile
    }


    bintray {
		user = rootProject.properties['bintray.user']
		key = rootProject.properties['bintray.apiKey']
		publications = [ 'mavenJava' ]
		pkg {
			repo = 'maven'
			userOrg = 'epages' // an optional organization name when the repo belongs to one of the user's orgs
			name = "epages-soapclient"
			desc = "AXIS 1.x based SOAP clients for epages6"
			licenses = ['Apache-2.0']
			labels = ['soap','epages']
		}
		dryRun = rootProject.properties['bintray.dryRun'] ?: false // whether to run this as dry-run, without deploying
	}
}