import org.apache.tools.ant.taskdefs.condition.Os

configure(subprojects.findAll {it.name.contains("perl/")}) {

    apply plugin: "eclipse"

    project.ext.osDependantPerl = {String[] args ->
        List<String> newArgs = new ArrayList<String>()
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            newArgs.add("cmd.exe")
            newArgs.add("/C")
            newArgs.add("perl")
            newArgs.addAll(args)
        } else {
            newArgs.add( System.env.PERL ?: "perl" )
            newArgs.addAll(args)
        }
        return newArgs.toArray(new String[0])
    }

    def putIfExists = { mymap, prop ->
        if(System.properties[prop] != null) {
            mymap.put(prop, System.properties[prop])
        }
    }

    task test(type:Exec) {
        description = 'Runs all webservice tests'
        group = 'Test'
        def envmap = [:]

        putIfExists(envmap, 'wsHostName')
        putIfExists(envmap, 'wsUrl')
        putIfExists(envmap, 'wsShop')
        putIfExists(envmap, 'wsUser')
        putIfExists(envmap, 'wsAppName')
        putIfExists(envmap, 'wsPassword')
        putIfExists(envmap, 'wsAppPassword')

        environment envmap
        logging.captureStandardOutput LogLevel.INFO
        workingDir projectDir.path
        commandLine osDependantPerl('runTests.pl')
    }

    task check (dependsOn: test)

    task clean << {
        delete "logs/communication.log"
    }

}